10 ' SAVE "TTT",A
20 SCREEN 3
30 CONSOLE ,,0:CLS 3
40 INPUT "t (mS)=",TN:TN=TN/1000
50 INPUT "時定数(mS)=",TC:TC=TC/1000
60 CLS
70 ' 条件データ
80 SF=20 'サンプリング周波数
90 SN=10 '平均データ数
100 XE=100 '最終値
110 TE=2  '最終時間(SEC.)
120 DF=30 '表示周波数
130 X0=38:Y0=346
140 DX=320/XE:TX=560/TE
150 DIM BUF1(TE*SF+1)
160 ' 縦軸表示
170 LINE (X0,5)-(X0,Y0),7
180 FOR I=0 TO 10
190  LOCATE 0,21-I*2
200  PRINT USING "####";XE/10*I;
210  LINE(X0,Y0-I*32)-(X0+580,Y0-I*32),7,,&H8888
220 NEXT
230 ' 横軸表示
240 LINE (X0,Y0)-(639,Y0),7
250 FOR I=0 TO 10
260  LOCATE I*7+1,22
270  T=TE/10*I:IF TE<10 THEN T=T*1000
280  PRINT USING "#####";T;
290  LINE (X0+I*56,Y0)-(X0+I*56,Y0-340),7,,&H8888
300 NEXT
310 LOCATE 50,23:IF TE<10 THEN PRINT "Time (mS)"; ELSE PRINT "Time (S)";
320 ' 条件データ表示
330 LINE (50*8, 6*16)-(69*8,11*16),0,BF
340 LINE (50*8, 6*16)-(69*8,11*16),7,B
350 LINE (52*8, 7*16+8)-(56*8, 7*16+8),6:LOCATE 58,7:PRINT "：原データ";
360 LINE (52*8, 8*16+8)-(56*8, 8*16+8),4:LOCATE 58,8:PRINT "：入力値";
370 LINE (52*8, 9*16+8)-(56*8, 9*16+8),5:LOCATE 58,9:PRINT "：フィルタ";
380 LINE (44*8,13*16)-(74*8,18*17),0,BF
390 LINE (44*8,13*16)-(74*8,18*17),7,B
400 LOCATE 45,14:PRINT "ステップ時間（ｔ）：";TN*1000;"ｍＳ";
410 LOCATE 45,15:PRINT "サンプリング周波数：";SF;"Ｈｚ";
420 LOCATE 45,16:PRINT "表示周波数        ：";DF;"Ｈｚ";
430 LOCATE 45,17:PRINT "時定数            ：";TC*1000;"ｍＳ";
440 ' 原データ描画
450 SP=0
460 PSET (X0,Y0),7
470 FOR T=0 TO TE+1/SF STEP 1/SF
480  IF T/TN<=XE THEN V=T/TN
490  Y=Y0-V*DX
500  X=T*TX+X0
510  LINE -(X,Y),6
520  BUF1(SP)=V:SP=SP+1
530 NEXT
540 ' 入力値描画
550 SP=0:YY=Y0
560 PSET (X0,Y0),7
570 FOR T=0 TO TE STEP 1/DF
580  V=BUF1(INT(SP+.000001))
590  Y=Y0-V*DX
600  X=T*TX+X0
610  LINE -(X,YY),4
620  LINE -(X,Y),4
630  SP=SP+SF/DF
640  YY=Y
650 NEXT
660 ' フィルタ値描画
670 SP=0:V=0:YY=Y0
680 PSET (X0,Y0),7
690 TT=1/30
700 FOR T=0 TO TE STEP 1/DF
710  V=(TT*BUF1(INT(SP+.000001))+TC*V)/(TC+TT)
720  X=T*TX+X0
730  Y=Y0-V*DX:SP=SP+SF/DF
735  LINE -(X,YY),5
740  LINE -(X,Y),5
745  YY=Y
750 NEXT
760 A$=INPUT$(1)
770 CLS 3
780 END
790 LOCATE 0,24:PRINT "印刷しますか？　（Ｙ／Ｎ） ";
800 Q$=INPUT$(1)
810 IF Q$="N" OR Q$="n" THEN PRINT "N";:GOTO 1000
820 IF Q$="Y" OR Q$="y" THEN LOCATE 0,24:PRINT "                          ";:GOTO 830 ELSE 800
830 ' 印刷
840 LOCATE 0,23:PRINT "DATA SUU";INT(TE*SF);"MABIKIRITU ";
850 INPUT M
860 LOCATE 0,23:PRINT "                                 ";
870 COPY 3
880 LPRINT CHR$(&HC);
890 LPRINT "時間（ｍＳ）  入力値     平均値     誤差(%)":LPRINT
900 SP=0
910 FOR T=0 TO TE-1/SF*M STEP 1/SF*M
920  LPRINT USING "    #####";T*1000;
930  LPRINT USING "    ####.##";BUF1(SP);
940  LPRINT USING "    ####.##";BUF2(SP);
950  IF T<>0 THEN LPRINT USING "     ###.##";ABS((BUF1(SP)-BUF2(SP))/BUF1(SP)*100);
960  LPRINT
970  SP=SP+M
980 NEXT
990 LPRINT CHR$(&HC);
1000 LOCATE 0,0
1010 END
