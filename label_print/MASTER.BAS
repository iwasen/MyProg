10 ' SAVE "MASTER"
20 '*********************************************************************
30 '*
40 '*	○○香料様向け　マスターファイル読み込みプログラム
50 '*
60 '*		FILE_NAME : MASTER.BAS
70 '*		AUTHOR    : S.AIZAWA
80 '*           DATE      : 90/11/22
90 '*********************************************************************
100 DEFINT A-Z
110 CLS
120 PRINT "         ＊＊＊　マスターファイル読み込みプログラム　＊＊＊"
130 PRINT
140 PRINT "フロッピーからハードディスクにマスターファイルを読み込みます。"
150 PRINT "指示に従ってフロッピーディスク上のファイル名を入力してください。"
160 PRINT "（リターンキーのみで何もせずに次のファイルに移ります）"
170 PRINT
180 INPUT "顧客マスターファイル名を入力して下さい ==>",FILE$
190 IF FILE$="" THEN 490
200 OPEN FILE$ FOR INPUT AS #1
210 OPEN "KOKYAKU.MST" AS #2
220 FIELD #2,3 AS FKCODE$(1),30 AS FKOKYAKU$(1)
230 FIELD #2,64 AS DMY$,3 AS FKCODE$(2),30 AS FKOKYAKU$(2)
240 FIELD #2,128 AS DMY$,3 AS FKCODE$(3),30 AS FKOKYAKU$(3)
250 FIELD #2,192 AS DMY$,3 AS FKCODE$(4),30 AS FKOKYAKU$(4)
260 PRINT "初期化中"
270 FOR I=1 TO 4
280   LSET FKCODE$(I)=SPACE$(3)
290   LSET FKOKYAKU$(I)=SPACE$(30)
300 NEXT
310 FOR I=1 TO 250
320   PUT #2,I
330 NEXT
340 PRINT "読み込み中"
350 CTRLZ=0
360 WHILE EOF(1)=0 AND CTRLZ=0
370   LINE INPUT #1,REC$
380   IF LEFT$(REC$,1)=CHR$(&H1A) THEN CTRLZ=1:GOTO 460
390   KCODE=VAL(MID$(REC$,1,3))
400   RECNO=KCODE\4+1
410   N=KCODE MOD 4+1
420   GET #2,RECNO
430   LSET FKCODE$(N)=MID$(REC$,1,3)
440   LSET FKOKYAKU$(N)=MID$(REC$,4,30)+"　　　　　　　　　　　　　　　　　　　　"
450   PUT #2,RECNO
460 WEND
470 CLOSE
480 PRINT "顧客マスター読み込み終了"
490 PRINT
500 INPUT "品目マスターファイル名を入力して下さい ==>",FILE$
510 IF FILE$="" THEN 910
520 OPEN FILE$ FOR INPUT AS #1
530 OPEN "HINMOKU.MST" AS #2
540 FIELD #2,4 AS FHCODE$(1),35 AS FHINMEI$(1),2 AS FKFLG$(1),1 AS FSFLG$(1)
550 FIELD #2,64 AS DMY$,4 AS FHCODE$(2),35 AS FHINMEI$(2),2 AS FKFLG$(2),1 AS FSFLG$(2)
560 FIELD #2,128 AS DMY$,4 AS FHCODE$(3),35 AS FHINMEI$(3),2 AS FKFLG$(3),1 AS FSFLG$(3)
570 FIELD #2,192 AS DMY$,4 AS FHCODE$(4),35 AS FHINMEI$(4),2 AS FKFLG$(4),1 AS FSFLG$(4)
580 OPEN "KAGAKU.MST" AS #3
590 FIELD #3,60 AS FKAGAKU$(1),60 AS FKAGAKU$(2),60 AS FKAGAKU$(3),60 AS FKAGAKU$(4)
600 PRINT "初期化中"
610 FOR I=1 TO 4
620   LSET FHCODE$(I)=SPACE$(4)
630   LSET FHINMEI$(I)=SPACE$(35)
640   LSET FKFLG$(I)=SPACE$(2)
650   LSET FSFLG$(I)=SPACE$(1)
660   LSET FKAGAKU$(I)="　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　"
670 NEXT
680 FOR I=1 TO 2500
690   PUT #2,I
700 NEXT
710 FOR I=1 TO 10000
720   PUT #3,I
730 NEXT
740 PRINT "読み込み中"
750 CTRLZ=0
760 WHILE EOF(1)=0 AND CTRLZ=0
770   LINE INPUT #1,REC$
780   IF LEFT$(REC$,1)=CHR$(&H1A) THEN CTRLZ=1:GOTO 880
790   HCODE=VAL(MID$(REC$,1,4))
800   RECNO=HCODE\4+1
810   N=HCODE MOD 4+1
820   GET #2,RECNO
830   LSET FHCODE$(N)=MID$(REC$,1,4)
840   LSET FHINMEI$(N)=MID$(REC$,5,30)
850   LSET FKFLG$(N)=MID$(REC$,35,2)
860   LSET FSFLG$(N)=MID$(REC$,37,1)
870   PUT #2,RECNO
880 WEND
890 CLOSE
900 PRINT "品目マスター読み込み終了"
910 PRINT
920 INPUT "取引マスターファイル名を入力して下さい ==>",FILE$
930 IF FILE$="" THEN 1330
940 OPEN FILE$ FOR INPUT AS #1
950 OPEN "TORxxxKI.MST" AS #2
960 FIELD #2,4 AS FTHCODE$(1),3 AS FTKCODE$(1),35 AS FAITE$(1),15 AS FTCODE$(1)
970 FIELD #2,64 AS DMY$,4 AS FTHCODE$(2),3 AS FTKCODE$(2),35 AS FAITE$(2),15 AS FTCODE$(2)
980 FIELD #2,128 AS DMY$,4 AS FTHCODE$(3),3 AS FTKCODE$(3),35 AS FAITE$(3),15 AS FTCODE$(3)
990 FIELD #2,192 AS DMY$,4 AS FTHCODE$(4),3 AS FTKCODE$(4),35 AS FAITE$(4),15 AS FTCODE$(4)
1000 PRINT "初期化中"
1010 TN=16381
1020 FOR I=1 TO 4
1030   LSET FTHCODE$(I)=SPACE$(4)
1040   LSET FTKCODE$(I)=SPACE$(3)
1050   LSET FAITE$(I)=SPACE$(35)
1060   LSET FTCODE$(I)=SPACE$(15)
1070 NEXT
1080 FOR I=1 TO (TN-1)/4+1
1090   PUT #2,I
1100 NEXT
1110 PRINT "読み込み中"
1120 CTRLZ=0
1130 WHILE EOF(1)=0 AND CTRLZ=0
1140   LINE INPUT #1,REC$
1150   IF LEFT$(REC$,1)=CHR$(&H1A) THEN CTRLZ=1:GOTO 1300
1160   HCODE=VAL(MID$(REC$,1,4))
1170   KCODE=VAL(MID$(REC$,5,3))
1180   RN=(HCODE+(HCODE MOD 200)*100+KCODE) MOD TN
1190   RNO=(RN+KCODE+HCODE) MOD TN
1200   IF RN=0 THEN RN=1
1210   RECNO=RNO\4+1
1220   N=RNO MOD 4+1
1230   GET #2,RECNO
1240   IF FTHCODE$(N)<>"    " THEN RNO=(RNO+RN) MOD TN:GOTO 1210
1250   LSET FTHCODE$(N)=MID$(REC$,1,4)
1260   LSET FTKCODE$(N)=MID$(REC$,5,3)
1270   LSET FAITE$(N)=MID$(REC$,8,30)
1280   LSET FTCODE$(N)=MID$(REC$,38,15)
1290   PUT #2,RECNO
1300 WEND
1310 CLOSE
1320 PRINT "取引マスター読み込み終了"
1330 PRINT
1340 PRINT "プログラム終了"
