10 ' SAVE "LABEL"
20 '*********************************************************************
30 '*
40 '*	○○香料様向け　ラベルプリントプログラム
50 '*
60 '*		FILE_NAME : LABEL.BAS
70 '*		AUTHOR    : S.AIZAWA
80 '*           DATE      : 90/11/22
90 '*********************************************************************
100 '--------------------------
110 ' 初期設定
120 '--------------------------
130 DEFINT A-Z
140 ON STOP GOSUB *OWARI:STOP ON
150 SCREEN 3,0:CONSOLE ,,0,1:COLOR 7,1,0,1,1:WIDTH 80,25:LOCATE ,,1
160 COLOR=(1,&H555):COLOR=(2,&H6):COLOR=(3,&HC):COLOR=(4,&HA00):COLOR=(6,&HCC0)
170 CR$=CHR$(&HD):UP$=CHR$(&H1E):DOWN$=CHR$(&H1F):ESC$=CHR$(&H1B)
180 DEF FNK(K$)=(K$>=CHR$(&H81) AND K$<=CHR$(&H9F) OR K$>=CHR$(&HE0) AND K$<=CHR$(&HF9))
190 TREC=16381   '取引マスターレコード数
200 DIM KIKEN$(100),SHOKUTEN$(10),KOURYOU$(10),SHIYOU$(10),SEIZOU$(10)
210 GOSUB *KFREAD
220 '--------------------------
230 ' 処理メニュー
240 '--------------------------
250 *MENU
260 SEL=1
270 TITLE$="《《《　ラベル発行プログラム　》》》":GOSUB *TITLE
280 LOCATE 24,4:PRINT "＊＊＊　処理メニュー　＊＊＊";
290 LOCATE 26,8:PRINT "１．ラベルプリント発行";
300 LOCATE 26,10:PRINT "２．マスターメンテナンス";
310 LOCATE 26,12:PRINT "３．補助";
320 LOCATE 26,15:PRINT "９．終了";
330 LOCATE 20,19:PRINT "処理を選択して下さい";
340 GOSUB *CLRFUNC:FKEY$(1)="  １  1"+CR$:FKEY$(2)="  ２  2"+CR$:FKEY$(3)="  ３  3"+CR$:FKEY$(9)="  ９  9"+CR$:GOSUB *SETFUNC
350 LOCATE 42,19:IF SEL<>4 THEN PRINT CHR$(SEL+&H30); ELSE PRINT "9";
360 IF SEL<>4 THEN LINE (24*8,(SEL*2+6)*16)-(52*8,(SEL*2+7)*16),4,BF ELSE LINE (24*8,15*16)-(52*8,16*16),4,BF
370 LOCATE 42,19:K$=INPUT$(1)
380 IF SEL<>4 THEN LINE (24*8,(SEL*2+6)*16)-(52*8,(SEL*2+7)*16),2,BF ELSE LINE (24*8,15*16)-(52*8,16*16),2,BF
390 IF K$="1" THEN SEL=1
400 IF K$="2" THEN SEL=2
410 IF K$="3" THEN SEL=3
420 IF K$="9" THEN SEL=4
430 IF K$=CHR$(&H1F) THEN SEL=SEL+1:IF SEL=5 THEN SEL=1
440 IF K$=CHR$(&H1E) THEN SEL=SEL-1:IF SEL=0 THEN SEL=4
450 IF K$=CHR$(&HD) THEN ON SEL GOSUB *LABEL,*MASTER,*HOJO,*OWARI:GOTO 270
460 GOTO 350
470 '--------------------------
480 ' ラベル発行
490 '--------------------------
500 *LABEL
510 TITLE$="《《《　ラベルプリント発行　》》》":GOSUB *TITLE
520 LOCATE 8,4:PRINT "品目番号 ［    ］   （Ｆ１：テストプリント　Ｆ９：終了）";
530 LOCATE 10,6:PRINT "品目名称   〈                                   〉";
540 LOCATE 10,8:PRINT "相手方呼称 〈                                   〉";
550 LOCATE 8,11:PRINT "顧客番号 ［   ］  〈                              〉";
560 LOCATE 8,14:PRINT "ＬＯＴ ＮＯ ［          ］      数量［       ］     枚数［   ］";
570 LOCATE 8,16:PRINT "食添証紙表示 （０：スル，１：シナイ） ［ ］";
580 LOCATE 8,18:PRINT "製造所在地  ［ ］　〈　　　　　　　　　　　　　　　　　　　　〉";
590 LOCATE 40,20:PRINT "確認 ［ ］ （ＯＫ＝０，ＮＯ＝１）";
600 GOSUB *HOPEN:GOSUB *KOPEN:GOSUB *TOPEN
610 GOSUB *PCLEAR
620 KCODE=0
630 '--------------------------
640 ' 品目コード入力
650 '--------------------------
660 GOSUB *CLRFUNC:FKEY$(1)="テスト"+ESC$+"1":FKEY$(9)=" 終了 "+ESC$+"9":GOSUB *SETFUNC
670 IX=19:IY=4:IMODE=0:IBUF$=HCODE$:GOSUB *INPUTS:HCODE$=IBUF$
680 IF ICH$=UP$ THEN 670
690 IF ICH$=ESC$ THEN A$=INPUT$(1):IF A$="1" THEN GOSUB *TESTPRINT ELSE IF A$="9" THEN CLOSE:RETURN
700 IF ICH$=CHR$(&H14) THEN GOSUB *TESTPRINT:GOTO 660
710 HCODE=VAL(HCODE$)
720 HCODE$=RIGHT$("000"+MID$(STR$(HCODE),2),4):LOCATE 19,4:PRINT HCODE$;
730 GOSUB *HREAD:LOCATE 23,6:IF HRFLG=0 THEN PRINT SPC(35); ELSE PRINT HINMEI$;
740 GOSUB *TREAD:LOCATE 23,8:IF TRFLG=0 THEN PRINT SPC(35); ELSE PRINT AITE$;
750 GOSUB *CLRFUNC:GOSUB *SETFUNC
760 '--------------------------
770 ' 顧客コード入力
780 '--------------------------
790 IX=19:IY=11:IMODE=0:IBUF$=KCODE$:GOSUB *INPUTS:KCODE$=IBUF$
800 IF ICH$=UP$ THEN 660
810 KCODE=VAL(KCODE$)
820 KCODE$=RIGHT$("00"+MID$(STR$(KCODE),2),3):LOCATE 19,11:PRINT KCODE$;
830 GOSUB *TREAD:LOCATE 23,8:IF TRFLG=0 THEN PRINT SPC(35); ELSE PRINT AITE$;
840 GOSUB *KREAD:LOCATE 28,11:IF KRFLG=0 THEN PRINT SPC(30); ELSE PRINT KOKYAKU$;
850 '--------------------------
860 ' ＬＯＴ ＮＯ．入力
870 '--------------------------
880 IX=22:IY=14:IMODE=1:IBUF$=LOTNO$:GOSUB *INPUTS:LOTNO$=IBUF$
890 IF ICH$=UP$ THEN 790
900 '--------------------------
910 ' 数量入力
920 '--------------------------
930 IX=46:IY=14:IMODE=1:IBUF$=SUURYOU$:GOSUB *INPUTS:SUURYOU$=IBUF$
950 IF ICH$=UP$ THEN 880
960 '--------------------------
970 ' 枚数入力
980 '--------------------------
990 IX=66:IY=14:IMODE=0:IBUF$=MAISUU$:GOSUB *INPUTS:MAISUU$=IBUF$
1000 RSET MAISUU$=STR$(VAL(MAISUU$)):LOCATE 66,14:PRINT MAISUU$;
1010 IF ICH$=UP$ THEN 930
1020 '--------------------------
1030 ' 食添証紙表示入力
1040 '--------------------------
1050 IX=48:IY=16:IMODE=0:IBUF$=SFLG$:GOSUB *INPUTS:SFLG$=IBUF$
1060 IF SFLG$<>"0" AND SFLG$<>"1" THEN 1050
1070 IF ICH$=UP$ THEN 990
1080 '--------------------------
1090 ' 製造所在地入力
1100 '--------------------------
1110 IX=22:IY=18:IMODE=0:IBUF$=SEIZOU$:GOSUB *INPUTS:SEIZOU$=IBUF$
1120 LOCATE 29,18:PRINT LEFT$(SEIZOU$(VAL(SEIZOU$)),20);
1130 IF ICH$=UP$ THEN 1050
1140 '--------------------------
1150 ' 確認入力
1160 '--------------------------
1170 GOSUB *CLRFUNC:FKEY$(1)=" ＯＫ 0"+CR$:FKEY$(10)=" ＮＧ 1"+CR$:GOSUB *SETFUNC
1180 IX=47:IY=20:IKEY$="01":GOSUB *INPCHR
1190 IF ICH$="1" THEN 660
1200 GOSUB *SOUSHIN
1210 GOTO 660
1220 '--------------------------
1230 ' 入力エリアクリア
1240 '--------------------------
1250 *PCLEAR
1260 HCODE$=SPACE$(4)
1270 KCODE$=SPACE$(3)
1280 LOTNO$=SPACE$(10)
1290 SUURYOU$=SPACE$(7)
1300 MAISUU$=SPACE$(3)
1310 SFLG$=SPACE$(1)
1320 SEIZOU$=SPACE$(1)
1330 RETURN
1340 '--------------------------
1350 ' 送信処理
1360 '--------------------------
1370 *SOUSHIN
1380 GOSUB *CLRFUNC:FKEY$(10)=" 中止 "+ESC$:GOSUB *SETFUNC
1390 LOCATE 14,20:COLOR 6:PRINT "【印刷中】";:COLOR 7
1400 OPEN "COM1:E81X" AS #6
1410 IF FMTFLG=0 THEN GOSUB *PUTFMT:FMTFLG=1
1420 K$=LEFT$(SHOKUTEN$(VAL(SFLG$)),16)+"　"+KOURYOU$(VAL(SFLG$))
1430 IF SFLG$="1" THEN K$="　　　　　　　　　　　　　"
1440 GOSUB *JISCNV:DT00$=KK$
1450 K$=KIKEN$(VAL(KFLG$)):GOSUB *JISCNV:DT01$=KK$
1460 IF TRFLG<>0 THEN DT14$=AITE$ ELSE DT14$=HINMEI$
1470 FOR I=LEN(DT14$) TO 1 STEP -1
1480   IF MID$(DT14$,I,1)<>" " THEN L=I:I=1
1490 NEXT
1500 IF L<=17 THEN WK14$="":FOR I=1 TO L:WK14$=WK14$+MID$(DT14$,I,1)+" ":NEXT:DT14$=WK14$:L=L*2-1
1505 DT14$=SPACE$((35-L)\2)+DT14$
1510 DT02$=HCODE$
1520 DT03$=TCODE$
1530 DT04$=MLBUF$(1):DT05$=MLBUF$(2):DT06$=MLBUF$(3):DT07$=MLBUF$(4)
1540 DT08$=LOTNO$
1550 DT09$=SUURYOU$
1560 K$="使用基準：":GOSUB *JISCNV:DT10$=KK$
1570 K$=SHIYOU$(VAL(SFLG$)):GOSUB *JISCNV:DT11$=KK$
1580 K$="製造者所在地：":GOSUB *JISCNV:DT12$=KK$
1590 K$=SEIZOU$(VAL(SEIZOU$)):GOSUB *JISCNV:DT13$=KK$
1600 MAISU$=RIGHT$("00"+MID$(STR$(VAL(MAISUU$)),2),3)
1610 GOSUB *PUTDAT
1620 CLOSE #6
1630 LOCATE 14,20:PRINT SPC(10);
1640 RETURN
1650 *TESTPRINT
1660 GOSUB *CLRFUNC:FKEY$(10)=" 中止 "+ESC$:GOSUB *SETFUNC
1670 LOCATE 14,20:COLOR 6:PRINT "【印刷中】";:COLOR 7
1680 OPEN "COM1:E81X" AS #6
1690 IF FMTFLG=0 THEN GOSUB *PUTFMT:FMTFLG=1
1700 K$="漢漢漢漢漢漢漢漢　漢漢漢漢":GOSUB *JISCNV:DT00$=KK$
1710 K$="漢漢漢漢漢漢漢漢漢漢漢漢漢漢漢漢漢漢漢漢":GOSUB *JISCNV:DT01$=KK$
1720 DT02$="AAAA"
1730 DT03$="AAAAAAAAAAAAAAA"
1740 K$="漢漢漢漢漢漢漢漢漢漢漢漢漢漢漢漢漢漢漢漢漢漢漢漢漢漢漢漢漢漢":GOSUB *JISCNV:DT04$=KK$
1750 DT05$=KK$
1760 DT06$=KK$
1770 DT07$=KK$
1780 DT08$="AAAAAAAAAA"
1790 DT09$="AAAAAAA"
1800 K$="使用基準：":GOSUB *JISCNV:DT10$=KK$
1810 K$="漢漢漢漢漢漢漢漢漢漢漢漢漢漢漢漢漢漢漢漢漢漢漢漢漢漢":GOSUB *JISCNV:DT11$=KK$
1820 K$="製造者所在地：":GOSUB *JISCNV:DT12$=KK$
1830 K$="漢漢漢漢漢漢漢漢漢漢漢漢漢漢漢漢漢漢漢漢漢漢漢漢":GOSUB *JISCNV:DT13$=KK$
1840 DT14$="AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA"
1850 MAISU$="001"
1860 GOSUB *PUTDAT
1870 CLOSE #6
1880 LOCATE 14,20:PRINT SPC(10);
1890 RETURN
1900 *JISCNV
1910 KK$=ESC$+"K"
1920 FOR K=1 TO KLEN(K$)
1930 J$=JIS$(KMID$(K$,K,1))
1940 KK$=KK$+CHR$(VAL("&H"+LEFT$(J$,2)))+CHR$(VAL("&H"+RIGHT$(J$,2)))
1950 NEXT
1960 KK$=KK$+ESC$+"O"
1970 RETURN
1980 *PUTFMT
1990 LN$=CHR$(&HA)+CHR$(&H0)
2000 FM$=ESC$+"D1030"+CHR$(&HA)+ESC$+"E0030"+LN$:GOSUB *RSPUT
2010 FM$=ESC$+"AX;I0000,I0000"+LN$:GOSUB *RSPUT
2020 FM$=ESC$+"T20C31"+LN$:GOSUB *RSPUT
2030 FM$=ESC$+"AY;I00,0"+LN$:GOSUB *RSPUT
2040 FM$=ESC$+"C"+LN$:GOSUB *RSPUT
2050 FM$=ESC$+"PC00;0235,0240,1,1,H,A01,00,B,J0100"+LN$:GOSUB *RSPUT
2060 FM$=ESC$+"PC01;0720,0240,1,1,H,A01,00,B,J0100"+LN$:GOSUB *RSPUT
2070 FM$=ESC$+"PC02;0320,0420,1,2,A,A01,00,B,J0000"+LN$:GOSUB *RSPUT
2080 FM$=ESC$+"PC03;0900,0420,1,2,A,A01,00,B,J0000"+LN$:GOSUB *RSPUT
2090 FM$=ESC$+"PC04;0370,0500,1,1,H,A01,00,B,J0000"+LN$:GOSUB *RSPUT
2100 FM$=ESC$+"PC05;0370,0535,1,1,H,A01,00,B,J0000"+LN$:GOSUB *RSPUT
2110 FM$=ESC$+"PC06;0370,0570,1,1,H,A01,00,B,J0000"+LN$:GOSUB *RSPUT
2120 FM$=ESC$+"PC07;0370,0605,1,1,H,A01,00,B,J0000"+LN$:GOSUB *RSPUT
2130 FM$=ESC$+"PC08;0350,0655,2,3,A,A01,00,B,J0000"+LN$:GOSUB *RSPUT
2140 FM$=ESC$+"PC09;0915,0655,2,3,A,A01,00,B,J0000"+LN$:GOSUB *RSPUT
2150 FM$=ESC$+"PC10;0345,0750,1,1,H,A01,00,B,J0100"+LN$:GOSUB *RSPUT
2160 FM$=ESC$+"PC11;0510,0750,1,1,H,A01,00,B,J0100"+LN$:GOSUB *RSPUT
2170 FM$=ESC$+"PC12;0345,0795,1,1,H,A01,00,B,J0100"+LN$:GOSUB *RSPUT
2180 FM$=ESC$+"PC13;0580,0795,1,1,H,A01,00,B,J0100"+LN$:GOSUB *RSPUT
2190 FM$=ESC$+"PV00;0225,0305,0030,0100,A,A2,00,B"+LN$:GOSUB *RSPUT
2200 RETURN
2210 *PUTDAT
2220 FM$=ESC$+"rC00;"+DT00$+LN$:GOSUB *RSPUT
2230 FM$=ESC$+"rC01;"+DT01$+LN$:GOSUB *RSPUT
2240 FM$=ESC$+"rC02;"+DT02$+LN$:GOSUB *RSPUT
2250 FM$=ESC$+"rC03;"+DT03$+LN$:GOSUB *RSPUT
2260 FM$=ESC$+"rC04;"+DT04$+LN$:GOSUB *RSPUT
2270 FM$=ESC$+"rC05;"+DT05$+LN$:GOSUB *RSPUT
2280 FM$=ESC$+"rC06;"+DT06$+LN$:GOSUB *RSPUT
2290 FM$=ESC$+"rC07;"+DT07$+LN$:GOSUB *RSPUT
2300 FM$=ESC$+"rC08;"+DT08$+LN$:GOSUB *RSPUT
2310 FM$=ESC$+"rC09;"+DT09$+LN$:GOSUB *RSPUT
2320 FM$=ESC$+"rC10;"+DT10$+LN$:GOSUB *RSPUT
2330 FM$=ESC$+"rC11;"+DT11$+LN$:GOSUB *RSPUT
2340 FM$=ESC$+"rC12;"+DT12$+LN$:GOSUB *RSPUT
2350 FM$=ESC$+"rC13;"+DT13$+LN$:GOSUB *RSPUT
2360 FM$=ESC$+"rV00;"+DT14$+LN$:GOSUB *RSPUT
2370 FM$=ESC$+"iN"+MAISU$+LN$:GOSUB *RSPUT
2380 RETURN
2390 *RSPUT
2400 PRINT #6,FM$;
2410 RETURN
2420 '--------------------------
2430 ' マスターメンテナンス
2440 '--------------------------
2450 *MASTER
2460 MSEL=1
2470 TITLE$="《《《　マスターメンテナンス　》》》":GOSUB *TITLE
2480 LOCATE 22,4:PRINT "＊＊＊　マスターファイル選択　＊＊＊";
2490 LOCATE 26,8:PRINT "１．品目マスターファイル";
2500 LOCATE 26,10:PRINT "２．顧客マスターファイル";
2510 LOCATE 26,12:PRINT "３．取引マスターファイル";
2520 LOCATE 26,15:PRINT "９．処理メニュー";
2530 LOCATE 20,19:PRINT "ファイルを選択して下さい";
2540 GOSUB *CLRFUNC:FKEY$(1)="  １  1"+CR$:FKEY$(2)="  ２  2"+CR$:FKEY$(3)="  ３  3"+CR$:FKEY$(9)="  ９  9"+CR$:GOSUB *SETFUNC
2550 LOCATE 46,19:IF MSEL<>4 THEN PRINT CHR$(MSEL+&H30); ELSE PRINT "9";
2560 IF MSEL<>4 THEN LINE (24*8,(MSEL*2+6)*16)-(51*8,(MSEL*2+7)*16),4,BF ELSE LINE (24*8,15*16)-(51*8,16*16),4,BF
2570 LOCATE 46,19:K$=INPUT$(1)
2580 IF MSEL<>4 THEN LINE (24*8,(MSEL*2+6)*16)-(51*8,(MSEL*2+7)*16),2,BF ELSE LINE (24*8,15*16)-(51*8,16*16),2,BF
2590 IF K$="1" THEN MSEL=1
2600 IF K$="2" THEN MSEL=2
2610 IF K$="3" THEN MSEL=3
2620 IF K$="9" THEN MSEL=4
2630 IF K$=CHR$(&H1F) THEN MSEL=MSEL+1:IF MSEL=5 THEN MSEL=1
2640 IF K$=CHR$(&H1E) THEN MSEL=MSEL-1:IF MSEL=0 THEN MSEL=4
2650 IF K$=CHR$(&HD) THEN IF MSEL<>4 THEN ON MSEL GOSUB *HINMOKU,*KOKYAKU,*TORxxxKI:GOTO 2470 ELSE RETURN
2660 GOTO 2550
2670 RETURN
2680 '--------------------------
2690 ' 品目マスターメンテナンス
2700 '--------------------------
2710 *HINMOKU
2720 TITLE$="《《《　品目マスターメンテナンス　》》》":GOSUB *TITLE
2730 LOCATE 8,4:PRINT "処理 ［ ］   （１．登録　２．修正　３．削除　４．表示　９．終了）";
2740 LOCATE 8,6:PRINT "コード ［    ］";
2750 LOCATE 8,8:PRINT "品名   ［                                   ］";
2760 LOCATE 8,10:PRINT "化学名及び含有量";
2770 LOCATE 8,12:PRINT " １［　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　］";
2780 LOCATE 8,13:PRINT " ２［　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　］";
2790 LOCATE 8,14:PRINT " ３［　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　］";
2800 LOCATE 8,15:PRINT " ４［　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　］";
2810 LOCATE 8,17:PRINT "危険物 ［  ］ 〈　　　　　　　　　　　　　　　　　　　　〉";
2820 LOCATE 8,19:PRINT "食添   ［ ］  〈　　　　　　　　　　　　　　　　　　　　〉";
2830 LOCATE 40,20:PRINT "確認 ［ ］ （ＯＫ＝０，ＮＯ＝１）";
2840 GOSUB *HOPEN
2850 '--------------------------
2860 ' 品目マスター処理番号入力
2870 '--------------------------
2880 HCODE$=SPACE$(4):GOSUB *HCLEAR:GOSUB *HDISPLAY
2882 LOCATE 28,6:PRINT SPC(30);
2885 GOSUB *CLRFUNC:FKEY$(1)=" 登録 1"+CR$:FKEY$(2)=" 修正 2"+CR$:FKEY$(3)=" 削除 3"+CR$:FKEY$(4)=" 表示 4"+CR$:FKEY$(9)=" 終了 9"+CR$:GOSUB *SETFUNC
2890 IX=15:IY=4:IKEY$="12349":GOSUB *INPCHR
2900 IF ICH$="9" THEN CLOSE:RETURN
2910 MODE$=ICH$
2940 '--------------------------
2950 ' 品目コード入力
2960 '--------------------------
2970 GOSUB *CLRFUNC:FKEY$(10)=" 中止 "+ESC$:GOSUB *SETFUNC
2975 HCODE$=SPACE$(4)
2980 IX=17:IY=6:IMODE=0:IBUF$=HCODE$:GOSUB *INPUTS:HCODE$=IBUF$
2990 LOCATE 28,6:PRINT SPC(30);
3000 IF ICH$=UP$ OR ICH$=ESC$ THEN 2880
3010 IF HCODE$="    " THEN 2970
3020 IF HCODE$="9999" THEN 2880
3030 HCODE=VAL(HCODE$)
3040 HCODE$=RIGHT$("000"+MID$(STR$(HCODE),2),4):LOCATE 17,6:PRINT HCODE$;
3050 ON VAL(MODE$) GOTO *HTOROKU,*HSHUUSEI,*HSAKUJO,*HHYOUJI
3060 '--------------------------
3070 ' 品目マスター登録
3080 '--------------------------
3090 *HTOROKU
3095 GOSUB *HCLEAR:GOSUB *HDISPLAY
3100 GOSUB *HREAD:IF HRFLG<>0 THEN GOSUB *HMSG1:GOTO 2970
3110 GOSUB *HINPUT:IF ICH$=ESC$ THEN 2880
3120 GOSUB *HKAKUNIN:IF ICH$="1" THEN 2970
3130 GOSUB *HWRITE:MSG$="【登録しました】":GOSUB *HMSG:GOTO 2970
3140 '--------------------------
3150 ' 品目マスター修正
3160 '--------------------------
3170 *HSHUUSEI
3180 GOSUB *HREAD
3190 GOSUB *HDISPLAY
3195 IF HRFLG=0 THEN GOSUB *HMSG2:GOTO 2970
3200 GOSUB *HINPUT:IF ICH$=ESC$ THEN 2880
3210 GOSUB *HKAKUNIN:IF ICH$="1" THEN 2970
3220 GOSUB *HWRITE:MSG$="【修正しました】":GOSUB *HMSG:GOTO 2970
3230 '--------------------------
3240 ' 品目マスター削除
3250 '--------------------------
3260 *HSAKUJO
3270 GOSUB *HREAD
3280 GOSUB *HDISPLAY
3285 IF HRFLG=0 THEN GOSUB *HMSG2:GOTO 2970
3290 GOSUB *HKAKUNIN:IF ICH$="1" THEN 2970
3300 HCODE$="    ":GOSUB *HWRITE:MSG$="【削除しました】":GOSUB *HMSG:GOTO 2970
3310 '--------------------------
3320 ' 品目マスター表示
3330 '--------------------------
3340 *HHYOUJI
3350 GOSUB *HREAD
3360 GOSUB *HDISPLAY
3365 IF HRFLG=0 THEN GOSUB *HMSG2
3370 GOTO 2970
3380 '------------------------------
3390 ' 品目マスターファイルオープン
3400 '------------------------------
3410 *HOPEN
3420 OPEN "HINMOKU.MST" AS #1
3430 FIELD #1,4 AS FHCODE$(1),35 AS FHINMEI$(1),2 AS FKFLG$(1),1 AS FSFLG$(1)
3440 FIELD #1,64 AS DMY$,4 AS FHCODE$(2),35 AS FHINMEI$(2),2 AS FKFLG$(2),1 AS FSFLG$(2)
3450 FIELD #1,128 AS DMY$,4 AS FHCODE$(3),35 AS FHINMEI$(3),2 AS FKFLG$(3),1 AS FSFLG$(3)
3460 FIELD #1,192 AS DMY$,4 AS FHCODE$(4),35 AS FHINMEI$(4),2 AS FKFLG$(4),1 AS FSFLG$(4)
3470 OPEN "KAGAKU.MST" AS #2
3480 FIELD #2,60 AS FKAGAKU$(1),60 AS FKAGAKU$(2),60 AS FKAGAKU$(3),60 AS FKAGAKU$(4)
3490 RETURN
3500 '----------------------------------
3510 ' 品目マスター読み込みサブルーチン
3520 '----------------------------------
3530 *HREAD
3540 GET #1,HCODE\4+1
3550 HN=HCODE MOD 4+1
3560 IF HCODE$<>FHCODE$(HN) THEN GOSUB *HCLEAR:HRFLG=0:RETURN
3570 HINMEI$=FHINMEI$(HN)
3580 KFLG$=FKFLG$(HN)
3590 SFLG$=FSFLG$(HN)
3600 HRFLG=1
3610 GET #2,HCODE+1
3620 MLBUF$(1)=FKAGAKU$(1)
3630 MLBUF$(2)=FKAGAKU$(2)
3640 MLBUF$(3)=FKAGAKU$(3)
3650 MLBUF$(4)=FKAGAKU$(4)
3660 RETURN
3670 '--------------------------------
3680 ' 品目マスタークリアサブルーチン
3690 '--------------------------------
3700 *HCLEAR
3720 HINMEI$=SPACE$(35)
3730 FOR I=1 TO 4
3740   MLBUF$(I)=SPACE$(60)
3750 NEXT
3760 KFLG$=SPACE$(2)
3770 SFLG$=SPACE$(1)
3780 RETURN
3790 '------------------------------
3800 ' 品目マスター表示サブルーチン
3810 '------------------------------
3820 *HDISPLAY
3830 LOCATE 17,6:PRINT HCODE$;
3840 LOCATE 17,8:PRINT HINMEI$;
3850 LOCATE 13,12:PRINT MLBUF$(1);
3860 LOCATE 13,13:PRINT MLBUF$(2);
3870 LOCATE 13,14:PRINT MLBUF$(3);
3880 LOCATE 13,15:PRINT MLBUF$(4);
3890 LOCATE 17,17:PRINT KFLG$;
3900 LOCATE 24,17:IF KFLG$<>"  " THEN PRINT KIKEN$(VAL(KFLG$)); ELSE PRINT SPC(40);
3910 LOCATE 17,19:PRINT SFLG$;
3920 LOCATE 24,19:IF SFLG$<>" " THEN PRINT SHOKUTEN$(VAL(SFLG$)); ELSE PRINT SPC(40);
3930 LOCATE 47,20:PRINT " ";
3940 RETURN
3950 '--------------------------
3960 ' 品目マスターデータ入力
3970 '--------------------------
3980 *HINPUT
3990 IX=17:IY=8:IMODE=1:IBUF$=HINMEI$:GOSUB *INPUTS:HINMEI$=IBUF$
4000 IF ICH$=UP$ THEN 3990
4010 IF ICH$=ESC$ THEN RETURN
4020 MLLI=1
4030 MLLP=12:MLCP=13:MLLN=4:MLCN=60:GOSUB *MLINPUT
4040 IF ICH$=UP$ THEN 3990
4050 IF ICH$=ESC$ THEN RETURN
4060 IX=17:IY=17:IMODE=0:IBUF$=KFLG$:GOSUB *INPUTS:KFLG$=IBUF$
4070 LOCATE 24,17:PRINT KIKEN$(VAL(KFLG$));
4080 IF ICH$=UP$ THEN MLLI=4:GOTO 4030
4090 IF ICH$=ESC$ THEN RETURN
4100 IX=17:IY=19:IMODE=0:IBUF$=SFLG$:GOSUB *INPUTS:SFLG$=IBUF$
4110 LOCATE 24,19:PRINT SHOKUTEN$(VAL(SFLG$));
4120 IF ICH$=UP$ THEN 4060
4130 IF ICH$=DOWN$ THEN 4100
4140 RETURN
4150 '--------------------------
4160 ' 品目マスター書き込み
4170 '--------------------------
4180 *HWRITE
4190 LSET FHCODE$(HN)=HCODE$
4200 LSET FHINMEI$(HN)=HINMEI$
4210 LSET FKFLG$(HN)=KFLG$
4220 LSET FSFLG$(HN)=SFLG$
4230 PUT #1,HCODE\4+1
4240 LSET FKAGAKU$(1)=MLBUF$(1)
4250 LSET FKAGAKU$(2)=MLBUF$(2)
4260 LSET FKAGAKU$(3)=MLBUF$(3)
4270 LSET FKAGAKU$(4)=MLBUF$(4)
4280 PUT #2,HCODE+1
4290 RETURN
4300 '--------------------------
4310 ' メッセージ表示
4320 '--------------------------
4330 *HMSG1
4340 MSG$="※ 登録済みです ※"
4350 GOTO *HMSG
4360 *HMSG2
4370 MSG$="※ 未登録です ※"
4380 *HMSG
4390 BEEP
4400 LOCATE 28,6:COLOR 6:PRINT MSG$;:COLOR 7
4410 RETURN
4420 '--------------------------
4430 ' 確認
4440 '--------------------------
4450 *HKAKUNIN
4460 GOSUB *CLRFUNC:FKEY$(1)=" ＯＫ 0"+CR$:FKEY$(10)=" ＮＧ 1"+CR$:GOSUB *SETFUNC
4470 IX=47:IY=20:IKEY$="01":GOSUB *INPCHR
4480 RETURN
4490 '--------------------------
4500 ' 顧客マスターメンテナンス
4510 '--------------------------
4520 *KOKYAKU
4530 TITLE$="《《《　顧客マスターメンテナンス　》》》":GOSUB *TITLE
4540 LOCATE 8,4:PRINT "処理 ［ ］   （１．登録　２．修正　３．削除　４．表示　９．終了）";
4550 LOCATE 8,7:PRINT "コード ［   ］";
4560 LOCATE 8,9:PRINT "顧客名 ［                              ］";
4570 LOCATE 40,19:PRINT "確認 ［ ］ （ＯＫ＝０，ＮＯ＝１）";
4580 GOSUB *KOPEN
4590 '--------------------------
4600 ' 顧客マスター処理番号入力
4610 '--------------------------
4620 KCODE$=SPACE$(3):GOSUB *KCLEAR:GOSUB *KDISPLAY
4622 LOCATE 28,7:PRINT SPC(30);
4625 GOSUB *CLRFUNC:FKEY$(1)=" 登録 1"+CR$:FKEY$(2)=" 修正 2"+CR$:FKEY$(3)=" 削除 3"+CR$:FKEY$(4)=" 表示 4"+CR$:FKEY$(9)=" 終了 9"+CR$:GOSUB *SETFUNC
4630 IX=15:IY=4:IKEY$="12349":GOSUB *INPCHR
4640 IF ICH$="9" THEN CLOSE:RETURN
4650 MODE$=ICH$
4680 '--------------------------
4690 ' 顧客コード入力
4700 '--------------------------
4710 GOSUB *CLRFUNC:FKEY$(10)=" 中止 "+ESC$:GOSUB *SETFUNC
4715 KCODE$=SPACE$(3)
4720 IX=17:IY=7:IMODE=0:IBUF$=KCODE$:GOSUB *INPUTS:KCODE$=IBUF$
4730 LOCATE 28,7:PRINT SPC(30);
4740 IF ICH$=UP$ OR ICH$=ESC$ THEN 4620
4750 IF KCODE$="   " THEN 4710
4760 IF KCODE$="999" THEN 4620
4770 KCODE=VAL(KCODE$)
4780 KCODE$=RIGHT$("00"+MID$(STR$(KCODE),2),3):LOCATE 17,7:PRINT KCODE$;
4790 ON VAL(MODE$) GOTO *KTOROKU,*KSHUUSEI,*KSAKUJO,*KHYOUJI
4800 '--------------------------
4810 ' 顧客マスター登録
4820 '--------------------------
4830 *KTOROKU
4835 GOSUB *KCLEAR:GOSUB *KDISPLAY
4840 GOSUB *KREAD:IF KRFLG<>0 THEN GOSUB *KMSG1:GOTO 4710
4850 GOSUB *KMINPUT:IF ICH$=ESC$ THEN 4620
4860 GOSUB *KKAKUNIN:IF ICH$="1" THEN 4710
4870 GOSUB *KWRITE:MSG$="【登録しました】":GOSUB *KMSG:GOTO 4710
4880 '--------------------------
4890 ' 顧客マスター修正
4900 '--------------------------
4910 *KSHUUSEI
4920 GOSUB *KREAD
4930 GOSUB *KDISPLAY
4935 IF KRFLG=0 THEN GOSUB *KMSG2:GOTO 4710
4940 GOSUB *KMINPUT:IF ICH$=ESC$ THEN 4620
4950 GOSUB *KKAKUNIN:IF ICH$="1" THEN 4710
4960 GOSUB *KWRITE:MSG$="【修正しました】":GOSUB *KMSG:GOTO 4710
4970 '--------------------------
4980 ' 顧客マスター削除
4990 '--------------------------
5000 *KSAKUJO
5010 GOSUB *KREAD
5020 GOSUB *KDISPLAY
5025 IF KRFLG=0 THEN GOSUB *KMSG2:GOTO 4710
5030 GOSUB *KKAKUNIN:IF ICH$="1" THEN 4710
5040 FKCODE$="   ":GOSUB *KWRITE:MSG$="【削除しました】":GOSUB *KMSG:GOTO 4710
5050 '--------------------------
5060 ' 顧客マスター表示
5070 '--------------------------
5080 *KHYOUJI
5090 GOSUB *KREAD
5100 GOSUB *KDISPLAY
5105 IF KRFLG=0 THEN GOSUB *KMSG2
5110 GOTO 4710
5120 '------------------------------
5130 ' 顧客マスターファイルオープン
5140 '------------------------------
5150 *KOPEN
5160 OPEN "KOKYAKU.MST" AS #3
5170 FIELD #3,3 AS FKCODE$(1),30 AS FKOKYAKU$(1)
5180 FIELD #3,64 AS DMY$,3 AS FKCODE$(2),30 AS FKOKYAKU$(2)
5190 FIELD #3,128 AS DMY$,3 AS FKCODE$(3),30 AS FKOKYAKU$(3)
5200 FIELD #3,192 AS DMY$,3 AS FKCODE$(4),30 AS FKOKYAKU$(4)
5210 RETURN
5220 '----------------------------------
5230 ' 顧客マスター読み込みサブルーチン
5240 '----------------------------------
5250 *KREAD
5260 GET #3,KCODE\4+1
5270 KN=KCODE MOD 4+1
5280 IF FKCODE$(KN)<>KCODE$ THEN GOSUB *KCLEAR:KRFLG=0:RETURN
5290 KOKYAKU$=FKOKYAKU$(KN)
5300 KRFLG=1
5310 RETURN
5320 '--------------------------------
5330 ' 顧客マスタークリアサブルーチン
5340 '--------------------------------
5350 *KCLEAR
5370 KOKYAKU$=SPACE$(30)
5380 RETURN
5390 '------------------------------
5400 ' 顧客マスター表示サブルーチン
5410 '------------------------------
5420 *KDISPLAY
5430 LOCATE 17,7:PRINT KCODE$;
5440 LOCATE 17,9:PRINT KOKYAKU$;
5450 LOCATE 47,19:PRINT " ";
5460 RETURN
5470 '--------------------------
5480 ' 顧客マスターデータ入力
5490 '--------------------------
5500 *KMINPUT
5510 IX=17:IY=9:IMODE=2:IBUF$=KOKYAKU$:GOSUB *INPUTS:KOKYAKU$=IBUF$
5520 IF ICH$<>CR$ AND ICH$<>ESC$ THEN 5510
5530 RETURN
5540 '--------------------------
5550 ' 顧客マスター書き込み
5560 '--------------------------
5570 *KWRITE
5580 LSET FKCODE$(KN)=KCODE$
5590 LSET FKOKYAKU$(KN)=KOKYAKU$
5600 PUT #3,KCODE\4+1
5610 RETURN
5620 '--------------------------
5630 ' メッセージ表示
5640 '--------------------------
5650 *KMSG1
5660 MSG$="※ 登録済みです ※"
5670 GOTO *KMSG
5680 *KMSG2
5690 MSG$="※ 未登録です ※"
5700 *KMSG
5710 BEEP
5720 LOCATE 28,7:COLOR 6:PRINT MSG$;:COLOR 7
5730 RETURN
5740 '--------------------------
5750 ' 確認
5760 '--------------------------
5770 *KKAKUNIN
5780 GOSUB *CLRFUNC:FKEY$(1)=" ＯＫ 0"+CR$:FKEY$(10)=" ＮＧ 1"+CR$:GOSUB *SETFUNC
5790 IX=47:IY=19:IKEY$="01":GOSUB *INPCHR
5800 RETURN
5810 '--------------------------
5820 ' 取引マスターメンテナンス
5830 '--------------------------
5840 *TORxxxKI
5850 TITLE$="《《《　取引マスターメンテナンス　》》》":GOSUB *TITLE
5860 LOCATE 8,4:PRINT "処理 ［ ］   （１．登録　２．修正　３．削除　４．表示　９．終了）";
5870 LOCATE 8,7:PRINT "品名コード   ［    ］";
5880 LOCATE 12,9:PRINT "品  名   〈                                   〉";
5890 LOCATE 8,11:PRINT "顧客コード   ［   ］";
5900 LOCATE 12,13:PRINT "顧客名   〈                              〉";
5910 LOCATE 8,15:PRINT "相手方呼称   ［                                   ］";
5920 LOCATE 8,17:PRINT "相手方コード ［                              ］";
5930 LOCATE 40,20:PRINT "確認 ［ ］ （ＯＫ＝０，ＮＯ＝１）";
5940 GOSUB *CLRFUNC:FKEY$(1)=" 登録 1"+CR$:FKEY$(2)=" 修正 2"+CR$:FKEY$(3)=" 削除 3"+CR$:FKEY$(4)=" 表示 4"+CR$:FKEY$(9)=" 終了 9"+CR$:GOSUB *SETFUNC
5950 GOSUB *HOPEN:GOSUB *KOPEN:GOSUB *TOPEN
5960 '--------------------------
5970 ' 取引マスター処理番号入力
5980 '--------------------------
5990 HCODE$=SPACE$(4):KCODE$=SPACE$(3):GOSUB *HCLEAR:GOSUB *KCLEAR:GOSUB *TCLEAR:GOSUB *TDISPLAY
5992 LOCATE 34,7:PRINT SPC(30);
5995 GOSUB *CLRFUNC:FKEY$(1)=" 登録 1"+CR$:FKEY$(2)=" 修正 2"+CR$:FKEY$(3)=" 削除 3"+CR$:FKEY$(4)=" 表示 4"+CR$:FKEY$(9)=" 終了 9"+CR$:GOSUB *SETFUNC
6000 IX=15:IY=4:IKEY$="12349":GOSUB *INPCHR
6010 IF ICH$="9" THEN CLOSE:RETURN
6020 MODE$=ICH$
6050 '--------------------------
6060 ' 品目コード入力
6070 '--------------------------
6080 GOSUB *CLRFUNC:FKEY$(10)=" 中止 "+ESC$:GOSUB *SETFUNC
6085 HCODE$=SPACE$(4)
6090 IX=23:IY=7:IMODE=0:IBUF$=HCODE$:GOSUB *INPUTS:HCODE$=IBUF$
6100 LOCATE 34,7:PRINT SPC(30);
6110 IF ICH$=UP$ OR ICH$=ESC$ THEN 5990
6120 IF HCODE$="    " THEN 6080
6130 IF HCODE$="9999" THEN 5990
6140 HCODE=VAL(HCODE$)
6150 HCODE$=RIGHT$("000"+MID$(STR$(HCODE),2),4):LOCATE 23,7:PRINT HCODE$;
6160 GOSUB *HREAD:LOCATE 23,9:PRINT HINMEI$;
6170 '--------------------------
6180 ' 顧客コード入力
6190 '--------------------------
6200 KCODE$=SPACE$(3)
6205 IX=23:IY=11:IMODE=0:IBUF$=KCODE$:GOSUB *INPUTS:KCODE$=IBUF$
6210 IF ICH$=UP$ THEN 6080
6220 IF ICH$=ESC$ THEN 5990
6230 IF KCODE$="   " THEN 6200
6240 IF KCODE$="999" THEN 6080
6250 KCODE=VAL(KCODE$)
6260 KCODE$=RIGHT$("00"+MID$(STR$(KCODE),2),3):LOCATE 23,11:PRINT KCODE$;
6270 GOSUB *KREAD:LOCATE 23,13:PRINT KOKYAKU$;
6280 ON VAL(MODE$) GOTO *TTOROKU,*TSHUUSEI,*TSAKUJO,*THYOUJI
6290 '--------------------------
6300 ' 取引マスター登録
6310 '--------------------------
6320 *TTOROKU
6325 GOSUB *TCLEAR:GOSUB *TDISPLAY
6330 GOSUB *TREAD:IF TRFLG<>0 THEN GOSUB *TMSG1:GOTO 6080
6340 GOSUB *TINPUT:IF ICH$=ESC$ THEN 5990
6350 GOSUB *TKAKUNIN:IF ICH$="1" THEN 6080
6360 IF DRFLG=1 THEN RNO=DRNO:TN=RNO MOD 4+1
6370 GOSUB *TWRITE:MSG$="【登録しました】":GOSUB *TMSG:GOTO 6080
6380 '--------------------------
6390 ' 取引マスター修正
6400 '--------------------------
6410 *TSHUUSEI
6420 GOSUB *TREAD
6430 GOSUB *TDISPLAY
6435 IF TRFLG=0 THEN GOSUB *TMSG2:GOTO 6080
6440 GOSUB *TINPUT:IF ICH$=ESC$ THEN 5990
6450 GOSUB *TKAKUNIN:IF ICH$="1" THEN 6080
6460 GOSUB *TWRITE:MSG$="【修正しました】":GOSUB *TMSG:GOTO 6080
6470 '--------------------------
6480 ' 取引マスター削除
6490 '--------------------------
6500 *TSAKUJO
6510 GOSUB *TREAD
6520 GOSUB *TDISPLAY
6525 IF TRFLG=0 THEN GOSUB *TMSG2:GOTO 6080
6530 GOSUB *TKAKUNIN:IF ICH$="1" THEN 6080
6540 HCODE$="****":GOSUB *TWRITE:MSG$="【削除しました】":GOSUB *TMSG:GOTO 6080
6550 '--------------------------
6560 ' 取引マスター表示
6570 '--------------------------
6580 *THYOUJI
6590 GOSUB *TREAD
6600 GOSUB *TDISPLAY
6605 IF TRFLG=0 THEN GOSUB *TMSG2
6610 GOTO 6080
6620 '------------------------------
6630 ' 取引マスターファイルオープン
6640 '------------------------------
6650 *TOPEN
6660 OPEN "TORxxxKI.MST" AS #4
6670 FIELD #4,4 AS FTHCODE$(1),3 AS FTKCODE$(1),35 AS FAITE$(1),15 AS FTCODE$(1)
6680 FIELD #4,64 AS DMY$,4 AS FTHCODE$(2),3 AS FTKCODE$(2),35 AS FAITE$(2),15 AS FTCODE$(2)
6690 FIELD #4,128 AS DMY$,4 AS FTHCODE$(3),3 AS FTKCODE$(3),35 AS FAITE$(3),15 AS FTCODE$(3)
6700 FIELD #4,192 AS DMY$,4 AS FTHCODE$(4),3 AS FTKCODE$(4),35 AS FAITE$(4),15 AS FTCODE$(4)
6710 RETURN
6720 '----------------------------------
6730 ' 取引マスター読み込みサブルーチン
6740 '----------------------------------
6750 *TREAD
6760 RN=(HCODE+(HCODE MOD 200)*100+KCODE) MOD TREC
6770 RNO=(RN+KCODE+HCODE) MOD TREC
6780 IF RN=0 THEN RN=1
6790 DRFLG=0
6800 TN=RNO MOD 4+1
6810 GET #4,RNO\4+1
6820 IF FTHCODE$(TN)="    " THEN GOSUB *TCLEAR:TRFLG=0:RETURN
6830 IF FTHCODE$(TN)="****" AND DRFLG=0 THEN DRNO=RNO:DRFLG=1
6840 IF HCODE$<>FTHCODE$(TN) OR KCODE$<>FTKCODE$(TN) THEN RNO=(RNO+RN) MOD TREC:GOTO 6800
6850 AITE$=FAITE$(TN)
6860 TCODE$=FTCODE$(TN)
6870 TRFLG=1
6880 RETURN
6890 '--------------------------------
6900 ' 取引マスタークリアサブルーチン
6910 '--------------------------------
6920 *TCLEAR
6970 AITE$=SPACE$(35)
6980 TCODE$=SPACE$(15)
6990 RETURN
7000 '------------------------------
7010 ' 取引マスター表示サブルーチン
7020 '------------------------------
7030 *TDISPLAY
7040 LOCATE 23,7:PRINT HCODE$;
7050 LOCATE 23,9:PRINT HINMEI$;
7060 LOCATE 23,11:PRINT KCODE$;
7070 LOCATE 23,13:PRINT KOKYAKU$;
7080 LOCATE 23,15:PRINT AITE$;
7090 LOCATE 23,17:PRINT TCODE$;
7100 LOCATE 47,20:PRINT " ";
7110 RETURN
7120 '--------------------------
7130 ' 取引マスターデータ入力
7140 '--------------------------
7150 *TINPUT
7160 IX=23:IY=15:IMODE=1:IBUF$=AITE$:GOSUB *INPUTS:AITE$=IBUF$
7170 IF ICH$=UP$ THEN 7160
7180 IF ICH$=ESC$ THEN RETURN
7190 IX=23:IY=17:IMODE=1:IBUF$=TCODE$:GOSUB *INPUTS:TCODE$=IBUF$
7200 IF ICH$=UP$ THEN 7160
7210 IF ICH$=DOWN$ THEN 7190
7220 RETURN
7230 '--------------------------
7240 ' 取引マスター書き込み
7250 '--------------------------
7260 *TWRITE
7270 LSET FTHCODE$(TN)=HCODE$
7280 LSET FTKCODE$(TN)=KCODE$
7290 LSET FAITE$(TN)=AITE$
7300 LSET FTCODE$(TN)=TCODE$
7310 PUT #4,RNO\4+1
7320 RETURN
7330 '--------------------------
7340 ' メッセージ表示
7350 '--------------------------
7360 *TMSG1
7370 MSG$="※ 登録済みです ※"
7380 GOTO *TMSG
7390 *TMSG2
7400 MSG$="※ 未登録です ※"
7410 *TMSG
7420 BEEP
7430 LOCATE 34,7:COLOR 6:PRINT MSG$;:COLOR 7
7440 RETURN
7450 '--------------------------
7460 ' 確認
7470 '--------------------------
7480 *TKAKUNIN
7490 GOSUB *CLRFUNC:FKEY$(1)=" ＯＫ 0"+CR$:FKEY$(10)=" ＮＧ 1"+CR$:GOSUB *SETFUNC
7500 IX=47:IY=20:IKEY$="01":GOSUB *INPCHR
7510 RETURN
7520 '--------------------------
7530 ' 補助
7540 '--------------------------
7550 *HOJO
7560 HSEL=1
7570 TITLE$="《《《　補助　》》》":GOSUB *TITLE
7580 LOCATE 22,4:PRINT "＊＊＊　補助機能メニュー　＊＊＊";
7590 LOCATE 22,8:PRINT "１．危険物ファイルメンテナンス";
7600 LOCATE 22,10:PRINT "２．食添ファイルメンテナンス";
7610 LOCATE 22,12:PRINT "３．製造者所在地ファイルメンテナンス";
7620 LOCATE 22,15:PRINT "９．処理メニュー";
7630 LOCATE 20,19:PRINT "処理を選択して下さい";
7640 GOSUB *CLRFUNC:FKEY$(1)="  １  1"+CR$:FKEY$(2)="  ２  2"+CR$:FKEY$(3)="  ３  3"+CR$:FKEY$(9)="  ９  9"+CR$:GOSUB *SETFUNC
7650 LOCATE 46,19:IF HSEL<>4 THEN PRINT CHR$(HSEL+&H30); ELSE PRINT "9";
7660 IF HSEL<>4 THEN LINE (20*8,(HSEL*2+6)*16)-(60*8,(HSEL*2+7)*16),4,BF ELSE LINE (20*8,15*16)-(60*8,16*16),4,BF
7670 LOCATE 46,19:K$=INPUT$(1)
7680 IF HSEL<>4 THEN LINE (20*8,(HSEL*2+6)*16)-(60*8,(HSEL*2+7)*16),2,BF ELSE LINE (20*8,15*16)-(60*8,16*16),2,BF
7690 IF K$="1" THEN HSEL=1
7700 IF K$="2" THEN HSEL=2
7710 IF K$="3" THEN HSEL=3
7720 IF K$="9" THEN HSEL=4
7730 IF K$=CHR$(&H1F) THEN HSEL=HSEL+1:IF HSEL=5 THEN HSEL=1
7740 IF K$=CHR$(&H1E) THEN HSEL=HSEL-1:IF HSEL=0 THEN HSEL=4
7750 IF K$=CHR$(&HD) THEN IF HSEL<>4 THEN ON HSEL GOSUB *KIKEN,*SHOKUTEN,*SEIZOU:GOTO 7570 ELSE RETURN
7760 GOTO 7650
7770 '----------------------------
7780 ' 危険物ファイルメンテナンス
7790 '----------------------------
7800 *KIKEN
7810 WIDTH 80,20
7820 TITLE$="《《《　危険物ファイルメンテナンス　》":GOSUB *TITLE2
7830 LINE (15*8,3*20)-(63*8,14*20),7,B
7840 LINE (21*8,3*20)-(21*8,14*20),7
7850 FOR I=1 TO 10
7860   LINE (15*8,(I+3)*20)-(63*8,(I+3)*20),7
7870 NEXT
7880 LOCATE 16,3:PRINT "ﾌﾗｸﾞ";
7890 LOCATE 33,3:PRINT "危  険  物  名  称"
7900 LOCATE 10,15:PRINT "項目選択：↑↓キー　　修正：ＣＲキー　　終了：Ｆ９";
7910 GOSUB *KKFUNC
7920 PAGE=0:LN=0
7930 GOSUB *KKDSP
7940 EDTFLG=0
7950 LINE (16*8,(4+LN)*20+1)-(20*8,(5+LN)*20-1),4,BF
7960 LOCATE 0,0,0:ICH$=INPUT$(1):LOCATE ,,1
7970 IF ICH$=CR$ THEN GOSUB *KKEDIT:GOTO 7960
7980 LINE (16*8,(4+LN)*20+1)-(20*8,(5+LN)*20-1),2,BF
7990 IF ICH$=UP$ THEN IF LN=0 THEN PAGE=(PAGE+9) MOD 10:LN=9:GOSUB *KKDSP ELSE LN=LN-1
8000 IF ICH$=DOWN$ THEN IF LN=9 THEN PAGE=(PAGE+1) MOD 10:LN=0:GOSUB *KKDSP ELSE LN=LN+1
8010 IF ICH$=CHR$(&H1D) THEN PAGE=(PAGE+9) MOD 10:GOSUB *KKDSP
8020 IF ICH$=CHR$(&H1C) THEN PAGE=(PAGE+1) MOD 10:GOSUB *KKDSP
8030 IF ICH$=ESC$ THEN GOSUB *KKWRITE:WIDTH 80,25:RETURN
8040 GOTO 7950
8050 *KKDSP
8060 FOR I=0 TO 9
8070   N=PAGE*10+I
8080   LOCATE 17,4+I:PRINT RIGHT$("0"+MID$(STR$(N),2),2);
8090   LOCATE 22,4+I:PRINT KIKEN$(N);
8100 NEXT
8110 RETURN
8120 *KKEDIT
8130 GOSUB *CLRFUNC:GOSUB *SETFUNC2
8140 IX=22:IY=LN+4:IMODE=2:IBUF$=KIKEN$(PAGE*10+LN):GOSUB *INPUTS:KIKEN$(PAGE*10+LN)=IBUF$
8150 GOSUB *KKFUNC
8160 EDTFLG=1
8170 RETURN
8180 *KKWRITE
8190 IF EDTFLG=0 THEN RETURN
8200 OPEN "KIKEN.DAT" FOR OUTPUT AS #5
8210 FOR I=0 TO 99
8220   PRINT #5,KIKEN$(I)
8230 NEXT
8240 CLOSE #5
8250 RETURN
8260 *KKFUNC
8270 GOSUB *CLRFUNC:FKEY$(6)=" 前頁 "+CHR$(&H1D):FKEY$(7)=" 次頁 "+CHR$(&H1C):FKEY$(9)=" 終了 "+ESC$:GOSUB *SETFUNC2
8280 RETURN
8290 '----------------------------
8300 ' 食添ファイルメンテナンス
8310 '----------------------------
8320 *SHOKUTEN
8330 WIDTH 80,20
8340 TITLE$="《《《　食添ファイルメンテナンス　》":GOSUB *TITLE2
8350 LINE (7*8,3*20)-(74*8,15*20),7,B
8360 LINE (12*8,3*20)-(12*8,15*20),7
8370 FOR I=1 TO 6
8380   LINE (7*8,I*40+20)-(74*8,I*40+20),7
8390   LINE (12*8,I*40+40)-(74*8,I*40+40),7,,&HCCCC
8400   LINE (54*8,I*40+20)-(54*8,I*40+40),7
8410   LINE (64*8,I*40+20)-(64*8,I*40+40),7
8420 NEXT
8430 LOCATE 8,3:PRINT "ﾌﾗｸﾞ";
8440 LOCATE 22,3:PRINT "食 品 添 加 物 名 称"
8450 LOCATE 56,3:PRINT "香  料"
8460 LOCATE 32,4:PRINT "使  用  基  準"
8470 LOCATE 10,16:PRINT "項目選択：↑↓キー　　修正：ＣＲキー　　終了：Ｆ９";
8480 GOSUB *STFUNC
8490 PAGE=0:LN=0
8500 GOSUB *STDSP
8510 EDTFLG=0
8520 LINE (8*8,(2+LN)*40+21)-(11*8,(2+LN)*40+39),4,BF
8530 LOCATE 0,0,0:ICH$=INPUT$(1):LOCATE ,,1
8540 IF ICH$=CR$ THEN GOSUB *STEDIT:GOTO 8530
8550 LINE (8*8,(2+LN)*40+21)-(11*8,(2+LN)*40+39),2,BF
8560 IF ICH$=UP$ THEN IF LN=0 THEN PAGE=(PAGE+1) MOD 2:LN=4:GOSUB *STDSP ELSE LN=LN-1
8570 IF ICH$=DOWN$ THEN IF LN=4 THEN PAGE=(PAGE+1) MOD 2:LN=0:GOSUB *STDSP ELSE LN=LN+1
8580 IF ICH$=CHR$(&H1D) THEN PAGE=(PAGE+1) MOD 2:GOSUB *STDSP
8590 IF ICH$=CHR$(&H1C) THEN PAGE=(PAGE+1) MOD 2:GOSUB *STDSP
8600 IF ICH$=ESC$ THEN GOSUB *STWRITE:WIDTH 80,25:RETURN
8610 GOTO 8520
8620 *STDSP
8630 FOR I=0 TO 4
8640   N=PAGE*5+I
8650   LOCATE 9,5+I*2:PRINT RIGHT$(STR$(N),1);
8660   LOCATE 13,5+I*2:PRINT SHOKUTEN$(N);
8670   LOCATE 55,5+I*2:PRINT KOURYOU$(N);
8680   LOCATE 13,6+I*2:PRINT SHIYOU$(N);
8690 NEXT
8700 RETURN
8710 *STEDIT
8720 GOSUB *CLRFUNC:GOSUB *SETFUNC2
8730 N=PAGE*5+LN
8740 IX=13:IY=LN*2+5:IMODE=2:IBUF$=SHOKUTEN$(N):GOSUB *INPUTS:SHOKUTEN$(N)=IBUF$
8750 IX=55:IY=LN*2+5:IMODE=2:IBUF$=KOURYOU$(N):GOSUB *INPUTS:KOURYOU$(N)=IBUF$
8760 IX=13:IY=LN*2+6:IMODE=2:IBUF$=SHIYOU$(N):GOSUB *INPUTS:SHIYOU$(N)=IBUF$
8770 GOSUB *STFUNC
8780 EDTFLG=1
8790 RETURN
8800 *STWRITE
8810 IF EDTFLG=0 THEN RETURN
8820 OPEN "SHOKUTEN.DAT" FOR OUTPUT AS #5
8830 FOR I=0 TO 9
8840   PRINT #5,SHOKUTEN$(I)
8850   PRINT #5,KOURYOU$(I)
8860   PRINT #5,SHIYOU$(I)
8870 NEXT
8880 CLOSE #5
8890 RETURN
8900 *STFUNC
8910 GOSUB *CLRFUNC:FKEY$(6)=" 前頁 "+CHR$(&H1D):FKEY$(7)=" 次頁 "+CHR$(&H1C):FKEY$(9)=" 終了 "+ESC$:GOSUB *SETFUNC2
8920 RETURN
8930 '----------------------------
8940 ' 製造者所在地ファイルメンテナンス
8950 '----------------------------
8960 *SEIZOU
8970 WIDTH 80,20
8980 TITLE$="《《《　製造者所在地ファイルメンテナンス　》":GOSUB *TITLE2
8990 LINE (7*8,3*20)-(74*8,14*20),7,B
9000 LINE (12*8,3*20)-(12*8,14*20),7
9010 FOR I=1 TO 10
9020   LINE (7*8,(I+3)*20)-(74*8,(I+3)*20),7
9030 NEXT
9040 LOCATE 8,3:PRINT "ﾌﾗｸﾞ";
9050 LOCATE 30,3:PRINT "製  造  者  所  在  地"
9060 LOCATE 10,16:PRINT "項目選択：↑↓キー　　修正：ＣＲキー　　終了：Ｆ９";
9070 GOSUB *SSFUNC
9080 LN=0
9090 GOSUB *SSDSP
9100 EDTFLG=0
9110 LINE (8*8,(4+LN)*20+1)-(11*8,(5+LN)*20-1),4,BF
9120 LOCATE 0,0,0:ICH$=INPUT$(1):LOCATE ,,1
9130 IF ICH$=CR$ THEN GOSUB *SSEDIT:GOTO 9120
9140 LINE (8*8,(4+LN)*20+1)-(11*8,(5+LN)*20-1),2,BF
9150 IF ICH$=UP$ THEN IF LN=0 THEN LN=9:GOSUB *SSDSP ELSE LN=LN-1
9160 IF ICH$=DOWN$ THEN IF LN=9 THEN LN=0:GOSUB *SSDSP ELSE LN=LN+1
9170 IF ICH$=ESC$ THEN GOSUB *SSWRITE:WIDTH 80,25:RETURN
9180 GOTO 9110
9190 *SSDSP
9200 FOR I=0 TO 9
9210   LOCATE 9,4+I:PRINT RIGHT$(STR$(I),1);
9220   LOCATE 13,4+I:PRINT SEIZOU$(I);
9230 NEXT
9240 RETURN
9250 *SSEDIT
9260 GOSUB *CLRFUNC:GOSUB *SETFUNC2
9270 IX=13:IY=LN+4:IMODE=2:IBUF$=SEIZOU$(LN):GOSUB *INPUTS:SEIZOU$(LN)=IBUF$
9280 GOSUB *SSFUNC
9290 EDTFLG=1
9300 RETURN
9310 *SSWRITE
9320 IF EDTFLG=0 THEN RETURN
9330 OPEN "SEIZOU.DAT" FOR OUTPUT AS #5
9340 FOR I=0 TO 9
9350   PRINT #5,SEIZOU$(I)
9360 NEXT
9370 CLOSE #5
9380 RETURN
9390 *SSFUNC
9400 GOSUB *CLRFUNC:FKEY$(9)=" 終了 "+ESC$:GOSUB *SETFUNC2
9410 RETURN
9420 '----------------------------
9430 ' 項目ファイル読み込み
9440 '----------------------------
9450 *KFREAD
9460 OPEN "KIKEN.DAT" FOR INPUT AS #5
9470 FOR I=0 TO 99
9480   LINE INPUT #5,KIKEN$(I)
9490 NEXT
9500 CLOSE #5
9510 OPEN "SHOKUTEN.DAT" FOR INPUT AS #5
9520 FOR I=0 TO 9
9530   LINE INPUT #5,SHOKUTEN$(I)
9540   LINE INPUT #5,KOURYOU$(I)
9550   LINE INPUT #5,SHIYOU$(I)
9560 NEXT
9570 CLOSE #5
9580 OPEN "SEIZOU.DAT" FOR INPUT AS #5
9590 FOR I=0 TO 9
9600   LINE INPUT #5,SEIZOU$(I)
9610 NEXT
9620 CLOSE #5
9630 RETURN
9640 '--------------------------
9650 ' 終了処理
9660 '--------------------------
9670 *OWARI
9680 KEY 1,"load "+CHR$(&H22):KEY 2,"auto ":KEY 3,"go to ":KEY 4,"list ":KEY 5,"run"+CR$:KEY 6,"save "+CHR$(&H22):KEY 7,"key ":KEY 8,"print ":KEY 9,"edit ."+CR$:KEY 10,"cont"+CR$
9690 WIDTH 80,25:CONSOLE ,,,,0:COLOR 7,0:CLS 3:LOCATE ,,1
9700 END
9710 '--------------------------
9720 ' タイトル表示
9730 '--------------------------
9740 *TITLE
9750 CLS 3
9760 FOR I=0 TO 4:LINE ((I*7+4)*8,23*16)-((I*7+10)*8,24*16),3,BF:NEXT
9770 FOR I=5 TO 9:LINE ((I*7+7)*8,23*16)-((I*7+13)*8,24*16),3,BF:NEXT
9780 LINE(8*4,16*2)-(8*76,16*22),6,BF,2
9790 LOCATE (80-LEN(TITLE$))/2,0:PRINT TITLE$;
9800 LOCATE 71,0:PRINT DATE$;
9810 RETURN
9820 *TITLE2
9830 CLS 3
9840 FOR I=0 TO 4:LINE ((I*7+4)*8,18*20+1)-((I*7+10)*8,19*20-3),3,BF:NEXT
9850 FOR I=5 TO 9:LINE ((I*7+7)*8,18*20+1)-((I*7+13)*8,19*20-3),3,BF:NEXT
9860 GOTO 9780
9870 '-------------------------------------------------
9880 ' 文字列入力サブルーチン
9890 '   入力  IX : 桁位置
9900 '         IY : 行位置
9910 '         IMODE : ０：数字　１：ＡＮＫ　２：漢字
9920 '   入出  IBUF$ : 入力文字列
9930 '   出力  ICH$ : 終了キー
9940 '-------------------------------------------------
9950 *INPUTS
9960 WLEN=LEN(IBUF$)
9970 WINS=0:WN=1
9980 LOCATE IX,IY
9990 *WINPUTS
10000 IF WN>WLEN THEN WN=WLEN:IF IMODE=2 THEN WN=WN-1
10010 LOCATE IX+WN-1,IY
10020 CONSOLE ,,,,1
10030 K$=INPUT$(1):IF FNK(K$) THEN K$=K$+INPUT$(1)
10040 CONSOLE ,,,,0
10050 IF K$=CR$ OR K$=UP$ OR K$=DOWN$ OR K$=ESC$ THEN ICH$=K$:RETURN
10060 IF K$=CHR$(&H12) THEN WINS=WINS XOR 1:GOTO *WINPUTS
10070 IF K$=CHR$(&H7F) THEN GOSUB *WINPDEL:GOTO *WINPUTS
10080 IF K$=CHR$(&H8) THEN GOSUB *WINPBS:GOTO *WINPUTS
10090 IF K$=CHR$(&H1D) THEN GOSUB *WINPLEFT:GOTO *WINPUTS
10100 IF K$=CHR$(&H1C) THEN GOSUB *WINPRIGHT:GOTO *WINPUTS
10110 IF K$>="0" AND K$<="9" AND IMODE=0 THEN GOSUB *WINPCHR:GOTO *WINPUTS
10120 IF (K$>=CHR$(&H20) AND K$<=CHR$(&H7E) OR K$>=CHR$(&HA0) AND K$<=CHR$(&HDF)) AND IMODE=1 THEN GOSUB *WINPCHR:GOTO *WINPUTS
10130 IF LEN(K$)=2 AND IMODE=2 THEN GOSUB *WINPCHR:GOTO *WINPUTS
10140 BEEP:GOTO *WINPUTS
10150 *WINPBS
10160 IF WN=1 THEN RETURN
10170 GOSUB *WINPLEFT
10180 LOCATE IX+WN-1,IY
10190 *WINPDEL
10200 WTYPE=FNK(MID$(IBUF$,WN,1))
10210 GOSUB *WINPDEL1:IF WTYPE THEN GOSUB *WINPDEL1
10220 PRINT MID$(IBUF$,WN);
10230 RETURN
10240 *WINPDEL1
10250 IF WN<WLEN THEN MID$(IBUF$,WN)=MID$(IBUF$,WN+1)
10260 IF IMODE<>2 THEN MID$(IBUF$,WLEN,1)=" " ELSE MID$(IBUF$,WLEN-1,2)="　"
10270 RETURN
10280 *WINPLEFT
10290 IF WN>1 THEN WN=WN-1:IF WN>1 THEN IF FNK(MID$(IBUF$,WN-1,1)) THEN WN=WN-1
10300 RETURN
10310 *WINPRIGHT
10320 IF WN<WLEN THEN WN=WN+1:IF FNK(MID$(IBUF$,WN-1,1)) THEN WN=WN+1
10330 RETURN
10340 *WINPCHR
10350 IF WN=WLEN AND LEN(K$)=2 THEN RETURN
10360 IF WINS=1 THEN 10400
10370 PRINT K$;
10380 IF LEN(K$)=1 AND FNK(MID$(IBUF$,WN,1)) THEN MID$(IBUF$,WN+1,1)=" ":PRINT " ";
10390 MID$(IBUF$,WN)=K$:WN=WN+LEN(K$):RETURN
10400 IF WN+LEN(K$)-1<WLEN THEN MID$(IBUF$,WN+LEN(K$))=MID$(IBUF$,WN)
10410 MID$(IBUF$,WN)=K$:IF FNK(MID$(IBUF$,WLEN,1)) THEN MID$(IBUF$,WLEN,1)=" "
10420 PRINT MID$(IBUF$,WN);:WN=WN+LEN(K$)
10430 RETURN
10440 '-------------------------------------------------
10450 ' １文字入力サブルーチン
10460 '   入力  IX : 桁位置
10470 '         IY : 行位置
10480 '         IKEY$ : 入力可能文字
10490 '   出力  ICH$ : 入力文字
10500 '-------------------------------------------------
10510 *INPCHR
10520 ICH$="":LOCATE IX,IY:PRINT " ";
10530 LOCATE IX,IY
10540 K$=INPUT$(1)
10550 IF K$=CHR$(&HD) THEN IF ICH$<>"" THEN RETURN ELSE BEEP
10560 IF INSTR(IKEY$,K$)<>0 THEN ICH$=K$:PRINT ICH$; ELSE BEEP
10570 GOTO 10530
10580 '-------------------------------------------------
10590 ' 複数行入力サブルーチン
10600 '   入力  MLLP:行位置 MLCP:桁位置 MLLN:行数 MLCN:桁数
10610 '   入出  MLBUF$:入力データ
10620 '   出力  ICH$ : 終了キー
10630 '-------------------------------------------------
10640 *MLINPUT
10650 WLP=MLLI:WCP=1:INS=0
10660 LOCATE MLCP+WCP-1,MLLP+WLP-1
10670 CONSOLE ,,,,1
10680 K$=INPUT$(1):IF FNK(K$) THEN K$=K$+INPUT$(1)
10690 CONSOLE ,,,,0
10700 ICH$=K$
10710 IF ICH$=ESC$ THEN RETURN
10720 IF ICH$=UP$ AND WLP=1 THEN RETURN
10730 IF (ICH$=CR$ OR ICH$=DOWN$) AND WLP=MLLN THEN RETURN
10740 GOSUB 10860:IF MLER<>0 THEN BEEP
10750 GOTO 10660
10760 '
10770 'データセット（INPUT : MLDT$=セットデータ）
10780 FOR I=1 TO LEN(MLDT$) STEP 2
10790  ICH$=MID$(MLDT$,I,2)
10800  LOCATE MLCP+WCP-1,MLLP+WLP-1
10810  GOSUB 10860
10820 NEXT
10830 RETURN
10840 '
10850 '１文字セットサブルーチン
10860 MLER=0
10870 IF LEN(ICH$)<>2 THEN 10910
10880 IF INS=0 THEN PRINT ICH$;:MID$(MLBUF$(WLP),WCP,2)=ICH$ ELSE GOSUB 11280
10890 WCP=WCP+2:GOSUB 11170
10900 RETURN
10910 IF ICH$=CHR$(&HD) THEN 11000      'ＲＥＴＫＥＹ
10920 IF ICH$=CHR$(&H8) THEN 11020      'ＢＳ　ＫＥＹ
10930 IF ICH$=CHR$(&H1E) THEN 11050     '  ↑  ＫＥＹ
10940 IF ICH$=CHR$(&H1D) THEN 11070     '  ←  ＫＥＹ
10950 IF ICH$=CHR$(&H1F) THEN 11090     '  ↓  ＫＥＹ
10960 IF ICH$=CHR$(&H1C) THEN 11110     '  →  ＫＥＹ
10970 IF ICH$=CHR$(&H12) THEN 11130     'ＩＮＳＫＥＹ
10980 IF ICH$=CHR$(&H7F) THEN 11150     'ＤＥＬＫＥＹ
10990 MLER=1:RETURN
11000 WCP=1:IF WLP<>MLLN THEN WLP=WLP+1
11010 RETURN
11020 IF WLP=1 AND WCP=1 THEN MLER=1:RETURN
11030 WCP=WCP-2:GOSUB 11170
11040 GOSUB 11200:RETURN
11050 IF WLP=1 THEN MLER=1 ELSE WLP=WLP-1
11060 RETURN
11070 IF WLP=1 AND WCP=1 THEN MLER=1 ELSE WCP=WCP-2:GOSUB 11170
11080 RETURN
11090 IF WLP=MLLN THEN MLER=1 ELSE WLP=WLP+1
11100 RETURN
11110 IF WLP=MLLN AND WCP>=MLCN-1 THEN MLER=1 ELSE WCP=WCP+2:GOSUB 11170
11120 RETURN
11130 INS=INS XOR 1
11140 RETURN
11150 GOSUB 11200
11160 RETURN
11170 IF WCP<1 THEN IF WLP>1 THEN WLP=WLP-1:WCP=MLCN-1 ELSE WCP=1
11180 IF WCP>MLCN THEN IF WLP<MLLN THEN WLP=WLP+1:WCP=1 ELSE WCP=MLCN-1
11190 RETURN
11200 WWCP=WCP
11210 FOR WWLP=WLP TO MLLN
11220  MID$(MLBUF$(WWLP),WWCP)=MID$(MLBUF$(WWLP),WWCP+2)
11230  IF WWLP<MLLN THEN MID$(MLBUF$(WWLP),MLCN-1,2)=LEFT$(MLBUF$(WWLP+1),2) ELSE MID$(MLBUF$(WWLP),MLCN-1,2)="　"
11240  LOCATE MLCP,MLLP+WWLP-1:PRINT MLBUF$(WWLP);
11250  WWCP=1
11260 NEXT
11270 RETURN
11280 WWCP=WCP:KKIN$=ICH$
11290 FOR WWLP=WLP TO MLLN
11300  WICH$=RIGHT$(MLBUF$(WWLP),2)
11310  IF WWCP+2<MLCN THEN MID$(MLBUF$(WWLP),WWCP+2)=MID$(MLBUF$(WWLP),WWCP)
11320  MID$(MLBUF$(WWLP),WWCP)=KKIN$
11330  KKIN$=WICH$
11340  LOCATE MLCP,MLLP+WWLP-1:PRINT MLBUF$(WWLP);
11350  WWCP=1
11360 NEXT
11370 RETURN
11380 '--------------------------------
11390 ' ファンクションキークリア
11400 '--------------------------------
11410 *CLRFUNC
11420 FOR I=1 TO 10:KEY I,"":FKEY$(I)="":NEXT
11430 RETURN
11440 '--------------------------------
11450 ' ファンクションキーセット
11460 '--------------------------------
11470 *SETFUNC
11480 FOR I=1 TO 5:LOCATE I*7-3,23:PRINT USING "&    &";FKEY$(I);:KEY I,MID$(FKEY$(I),7):NEXT
11490 FOR I=6 TO 10:LOCATE I*7,23:PRINT USING "&    &";FKEY$(I);:KEY I,MID$(FKEY$(I),7):NEXT
11500 RETURN
11510 *SETFUNC2
11520 FOR I=1 TO 5:LOCATE I*7-3,18:PRINT USING "&    &";FKEY$(I);:KEY I,MID$(FKEY$(I),7):NEXT
11530 FOR I=6 TO 10:LOCATE I*7,18:PRINT USING "&    &";FKEY$(I);:KEY I,MID$(FKEY$(I),7):NEXT
11540 RETURN
